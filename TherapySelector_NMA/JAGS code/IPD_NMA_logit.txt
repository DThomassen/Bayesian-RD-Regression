model {

    for(s in 1:NS) { # loop over studies
        for (a in 1:narms[s])  { # loop over arms
          for (y in 1:npt[s,a]) { # loops over pts in this arm
          
            SVR[s,a,y] ~ dbin(p[s,t[s,a],y], 1)
            logit(p[s,t[s,a],y]) <- mu[s] + delta[s,t[s,a]]
          }
        }
        
        delta[s,t[s,1]]<-0
        w[s,1] <- 0
        
        for (a in 2:narms[s]) {
	        ## normally-distributed random treatment effects
            delta[s,t[s,a]] ~ dnorm(md[s,t[s,a]],taud[s,t[s,a]])
            md[s,t[s,a]] <- d[t[s,a]] - d[t[s,1]] + sw[s,a]

            ## adjustment for correlation within trials with 3 arms or more
            sw[s,a] <- sum(w[s,1:(a-1)])/(a-1)
            w[s,a] <- (delta[s,t[s,a]]  - d[t[s,a]] + d[t[s,1]])

            ## random effects 1/variance constrained to be the same for every comparison
            taud[s,t[s,a]] <- tau*2*(a-1)/a
        }
    }
    
    for (i in 1:NS){
        mu[i] ~ dnorm(0,0.0001) # vague prior for baseline log-odds
    }
    
    # log odds ratio compared to treatment 1 (e.g. placebo)
    d[1] <- 0  
    for (i in 2:NT) {
        d[i] ~ dnorm(0, 0.04)
    }
    
    tau <- 1/pow(sd, 2)
    sd ~ dunif(0, 5)


##  Log odds of SVR under treatment 1
    for (i in 1:length(SVR_Ref)){
        SVR_Ref[i] ~ dbin(SVR_Ref_prob,1)
    }
    logit(SVR_Ref_prob) <- alpha
    alpha ~ dnorm(0,0001)

##  Absolute probability of SVR under each intervention
    for (i in 1:NT) {
        logit(pq[i]) <- alpha + d[i]
    }
    
    for (i in 1:NT) {
        RD[i] <- pq[i] - SVR_Ref_prob
    }
    
    
}